description = 'Spring Cloud Connector Extension for Pivotal Single Sign On Service on Pivotal Cloud Foundry'

buildscript {
  repositories {
    maven { url "https://repo.spring.io/plugins-release" }
    mavenCentral()
  }

  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    classpath("io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE")
    classpath("io.spring.gradle:spring-io-plugin:0.0.4.RELEASE")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Angel.SR3"
  }
}

javadoc {
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
  options.author = true
  options.header = project.name
}

task packageSources(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  classifier = "javadoc"
  from javadoc
}

archivesBaseName = "spring-cloud-sso-connector"
group = "io.pivotal.spring.cloud"
version = "1.0.0.BUILD-SNAPSHOT"

dependencies {
  compile 'org.springframework.cloud:spring-cloud-starter-oauth2'
  compile 'org.springframework.cloud:spring-cloud-spring-service-connector'
  compile 'org.springframework.cloud:spring-cloud-cloudfoundry-connector'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: "org.springframework.cloud", name: "spring-cloud-cloudfoundry-connector", version: "1.1.1.RELEASE", classifier: "tests"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

